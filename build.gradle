plugins {
  id 'java-gradle-plugin'
  id 'groovy'
  id 'maven'
  id 'signing'
  id 'idea'
  id 'maven-publish'
  id 'com.jfrog.artifactory' version '4.9.6'
}

def project_description = """\
A Gradle plugin utilizing Capsule, the packaging and deployment tool for JVM apps.
(http://www.capsule.io)
"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
}

dependencies {
    compile gradleApi()
    compile localGroovy()
    testCompile group: 'org.testng', name: 'testng', version: '6.10'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives sourcesJar
}

gradlePlugin {
    plugins {
        capsulePlugin {
            id = 'us.kirchmeier.capsule'
            implementationClass = 'us.kirchmeier.capsule.CapsuleGradlePlugin'
        }
    }
}

test {
    useTestNG()
}

def publishedProjects = projects.project

configure(publishedProjects) { subproject ->
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.artifactory'

    task sourceJar(type: Jar, dependsOn: subproject.classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    publishing {
        publications {
            create(subproject.name, MavenPublication) {

                if (subproject.hasProperty("mavenArtifacts")) {
                    subproject.mavenArtifacts.call(it)
                } else {
                    from components.java
                }

                groupId subproject.group
                artifactId subproject.name
                artifact tasks.sourceJar

                pom {
                    name = subproject.name
                    packaging = 'jar'
                    description = subproject.description
                }
            }
        }
    }
}

artifactory {
    publish {
        contextUrl = 'https://ci-artifactory.corda.r3cev.com/artifactory'
        repository {
            repoKey = 'corda-dependencies'
            username = System.getenv('CORDA_ARTIFACTORY_USERNAME') ?: System.getProperty('corda.artifactory.username')
            password = System.getenv('CORDA_ARTIFACTORY_PASSWORD') ?: System.getProperty('corda.artifactory.password')
        }

        defaults {
            publications(project.name)
        }
    }
}
